package com.ai

import com.ai.study.integral.IntegralBean;

rule birthdayPoint
	// 过生日，则加10分，并且将当月交易比数翻倍后再计算积分
	salience 100
	lock-on-active true
	when
		$integralBean : IntegralBean(birthDay == true)
	then
		$integralBean.setPoint($integralBean.getPoint()+10);
		$integralBean.recordPointLog($integralBean.getUserName(),"birthdayPoint",$integralBean.getPoint());
end

rule billThisMonthPoint
	// 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
	salience 99
	lock-on-active true
	date-effective "2011-01-08 23:59:59"
	date-expires "2011-08-08 23:59:59"
	when
		$integralBean : IntegralBean(billThisMonth >= 3)
	then
		$integralBean.setPoint($integralBean.getPoint()+$integralBean.getBillThisMonth()/3*30);
		$integralBean.recordPointLog($integralBean.getUserName(),"billThisMonthPoint",$integralBean.getPoint());
end

rule buyMoneyPoint
	// 当月购物总金额100以上，每100元赠送10分
	salience 98
	lock-on-active true
	when
		$integralBean : IntegralBean(buyMoney >= 100)
	then
		$integralBean.setPoint($integralBean.getPoint()+ (int)$integralBean.getBuyMoney()/100 * 10);
		$integralBean.recordPointLog($integralBean.getUserName(),"buyMoneyPoint",$integralBean.getPoint());
end

rule buyNumsPoint
	// 当月购物次数5次以上，每五次赠送50分
	salience 97
	lock-on-active true
	when
		$integralBean : IntegralBean(buyNums >= 5)
	then
		$integralBean.setPoint($integralBean.getPoint()+$integralBean.getBuyNums()/5 * 50);
		$integralBean.recordPointLog($integralBean.getUserName(),"buyNumsPoint",$integralBean.getPoint());
end

rule allFitPoint
	// 特别的，如果全部满足了要求，则额外奖励100分
	salience 96
	lock-on-active true
	when
		$integralBean:IntegralBean(buyNums >= 5 && billThisMonth >= 3 && buyMoney >= 100)
	then
		$integralBean.setPoint($integralBean.getPoint()+ 100);
		$integralBean.recordPointLog($integralBean.getUserName(),"allFitPoint",$integralBean.getPoint());
end